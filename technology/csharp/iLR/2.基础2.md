**bgt.s**

```
.class public ConsoleApplication1.Program
{
	.method public static bool IsBiggerThan( int32, int32)
    {
        .maxstack 2    // []
        ldarg.0        // [4]
        ldarg.1        // [8,4]
        bgt.s Bigger   // []
        ldc.i4.0       // [0]
        ret            // []
        Bigger:
        ldc.i4.1       // [1]
        ret            // []
    }
}
```

1. 执行到bgt.s时，将8,4依次弹出堆栈，判断4是否大于8，大于则跳转到Bigger

2. 这里将0压入栈顶表示false，非0表示true

**本地变量**

 

```
.class public Tutorial02.Program
{
    .method public static int32 AddWithLocal( int32 a, int32 b)
    {
        .maxstack 2
        .locals init(			//变量声明应放在maxstack 2 后，指令前
            int32 result
        )
 
        ldarg.0					//载入参数0
        ldarg.1					//载入参数1
        add
 
        stloc.0		//弹出栈顶值至index为0的本地变量 等同于 stloc.s 0 或 stloc.s result
        ldloc.0		//将index为0的本地变量压入栈顶 等同于 ldloc.s 0 或 ldloc.s result
        ret
    }
}
```

**Loop**

```
.method public static int32 ToPower(int32 base, int32 exponent)
{
    .maxstack 2				//最大堆栈使用 2个就够了
    .locals init (
        [0] int32 result,
        [1] int32 i
     )

     //load 1 to result then 0 to i
     ldc.i4.1			//1压入栈顶
     stloc.0			//弹出栈顶值1 赋给 result
     ldc.i4.0			//0压入堆栈
     stloc.1			//弹出栈顶值0 赋给 i

     //start of the loop
     br.s CheckCondition	//跳转分支 CheckCondition

     LoopStart:
     ldloc.0				//将局部变量index为0的result 压入栈顶
     ldarg.0				//参数0 base 压入栈顶
     mul					//弹出栈顶两值 相乘 结果压入栈顶
     stloc.0				//栈顶弹出值 赋给 result

     ldloc.1				//将局部变量index为0的i 压入栈顶
     ldc.i4.1				//将值1 压入栈顶
     add					//弹出栈顶两值 相加 结果压入栈顶
     stloc.1				//栈顶弹出值 赋给 局部变量index为1的 i

     //Checking the loop condition
     CheckCondition:
     ldloc.1				//i 压入栈顶
     ldarg.1				//参数1 exponent 压入栈顶
     blt.s LoopStart		//弹出栈顶两值比大小 i < exponent 则跳转到 LoopStart
     //end of the loop

     ldloc.0				//将result压入栈顶
     ret
}
```

上述代码其实实现的是  base的exponent次方

**box**

    .method public static void Boxing( int32)
    {
        .maxstack 2
     
        ldstr "The boxed value is: {0}"
        ldarg.0
        box [mscorlib]System.Int32			//装箱 栈顶放引用地址
        call void [mscorlib]System.Console::WriteLine( string, object)
     
        ret
    }
**unbox**

```
.method public static int32 UnboxInt(object)
{
    .maxstack 1
	ldarg.0									//栈顶压入object类型参数 的引用
    unbox.any [mscorlib]System.Int32		//拆箱 此时栈顶由引用变为引用指向的int值

    ret
}
```

**out and ref**  ->out

    .method public static bool TryParse( string s, [out] int32& result)
    {
        ldarg.1    // [result&]     将inde为1的参数压入堆栈
        ldc.i4.2   // [2, result&]	将值2压入堆栈
        stind.i4   // []			弹出栈顶 值和地址，将值作为int32(i4)存入内存地址
     
        ldc.i4.1   // [1]			栈顶压入1 表示true
        ret        // []
    }
这里模拟了TryParse做的操作，将值2放进了内存地址&result

stind.i4  弹出栈顶 值和地址，将值作为int32(i4)存入内存地址

**out and ref**  ->ref

```
.method public static void SwapWithRefParameter( int32& a, int32& b)
{
    .locals init(
        [0] int32 temp
    )

    ldarg.0    // [a&]	将index为0的参数 a的地址 压入堆栈
    ldind.i4   // [a]	弹出栈顶的值作为地址取出值作为int32(i4)压入堆栈
    stloc.0    // []	将栈顶值弹出存入index为0的局部变量(temp)

    ldarg.0    // [a&]			将index为0的参数 a的地址 压入堆栈
    ldarg.1    // [b&, a&]		将index为1的参数 b的地址 压入堆栈
    ldind.i4   // [b, a&]		弹出栈顶的值作为地址取出值作为int32(i4)压入堆栈
    stind.i4   // []			弹出栈顶 值和地址，将值存入内存地址

    ldarg.1    // [b&]			将index为1的参数 b的地址 压入堆栈
    ldloc.0    // [temp, b&]	将index为0的局部变量的值压入堆栈
    stind.i4   // []			弹出栈顶 值和地址，将值存入内存地址

    ret
}
```

这里交换了a和b的值。

ldind.i4 弹出栈顶的值作为地址取出值作为int32(i4)压入堆栈